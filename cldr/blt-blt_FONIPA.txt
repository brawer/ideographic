# References
# ----------
# [1] Jim Brase, SIL International: Proposal to encode the Tai Viet script
#     in the UCS. ISO/IEC JTC1/SC2/WG2 N3220. March 20, 2007.
#     http://std.dkuug.dk/jtc1/sc2/wg2/docs/n3220.pdf
#
# [2] ScriptSource: Tai Viet Vowels.
#     http://scriptsource.org/entry/eusd5ehysa
#
# [3] http://www.seasite.niu.edu/tai/TaiDam/tones.htm

$LO = [ꪀ ꪂ ꪄ ꪆ ꪈ ꪊ ꪌ ꪎ ꪐ ꪒ ꪔ ꪖ ꪘ ꪚ ꪜ ꪞ ꪠ ꪢ ꪤ ꪦ ꪨ ꪪ ꪬ ꪮ];
$HI = [ꪁ ꪃ ꪅ ꪇ ꪉ ꪋ ꪍ ꪏ ꪑ ꪓ ꪕ ꪗ ꪙ ꪛ ꪝ ꪟ ꪡ ꪣ ꪥ ꪧ ꪩ ꪫ ꪭ ꪯ];
$C = [$LO $HI];
$V1 = [ꪵ ꪶ ꪹ ꪻ ꪼ];  # vowels written before consonant
$V2 = [ ꪰ ꪲ ꪳ ꪴ ꪷ ꪸ ꪾ];  # vowels written above or below consonant
$V = [$V1 $V2];
$W = [ꪫ];  # labialization marker
$F = [$C]; # syllable-final consonant

# The Tai Viet script uses visual ordering. Convert to logical order.
($V1) ($C $W?) → $2 $1;
::null;

# Convert tones.
# TODO: This only works for "unchecked" syllables.
# TODO: Also support tone letters, not just tone marks.
$LO $W? $V { ꪿ ($F?)} → $1 ˧˥;  # Tone class 2: High-rising tone
$LO $W? $V { ꫁ ($F?)} → $1 ˨˩;  # Tone class 3: Low-falling tone
$HI $W? $V { ꪿ ($F?)} → $1 ˦;   # Tone class 5: High-mid tone
$HI $W? $V { ꫁ ($F?)} → $1 ˧˩;  # Tone class 6: Mid-falling tone
::null;
$NOTONE = [^˥˦˧˨˩];
{($LO $W? $V $F?)} $NOTONE → $1 ˨;  # Tone class 1: Low-mid tone.
{($HI $W? $V $F?)} $NOTONE → $1 ˥;  # Tone class 4: High tone.
::null;

# Harden syllable-final consonants.
$C $W? $V {ꪒ} → ꪔ;  # /d/ → /t/
::null;

# Convert labialization marker.
$C {$W} $V → ʷ;
::null;

[ꪀ ꪁ] → k;
[ꪄ ꪅ] → x;
[ꪎ ꪏ] → s;
[ꪐ ꪑ] → ɲ;
[ꪒ ꪓ] → d;
[ꪔ ꪕ] → t;
[ꪘ ꪙ] → n;
ꪫ → w;


# Vowels.
 ꪰ → a;
ꪵ → ɛ;
ꪶ → o;
 ꪳ → i;
ꪹ → ɨə̯;
ꪻ → əw;
ꪼ → ai̯;

# Word ligature symbols.
ꫛ → kon˥;
ꫜ → nɨŋ˦;
